apply plugin: 'groovy'

sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy']
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
        groovy {
            srcDirs = ['src/test/groovy']
        }
    }
    jobs {
        groovy {
            srcDirs = ['src/jobs']
        }
    }
}

repositories {
    maven { url 'https://repo.jenkins-ci.org/releases/' }
    mavenCentral()
    jcenter()
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.17'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation 'org.jenkins-ci.main:jenkins-core:2.411'
    implementation 'org.jenkins-ci.main:jenkins-test-harness:2042.v787a_641a_9b_26'
    implementation 'org.jenkins-ci.main:jenkins-war:2.411'

    testImplementation 'junit:junit:4.13'
    testImplementation 'commons-io:commons-io:2.11.0'

    // Job DSL plugin including plugin dependencies
    testImplementation "org.jenkins-ci.plugins:job-dsl:1.83"
    testImplementation "org.jenkins-ci.plugins:job-dsl:1.83@jar"
    testImplementation 'org.jenkins-ci.plugins:structs:1.20@jar'
    implementation 'org.jenkins-ci.plugins:job-dsl-core:1.83'

    // testImplementation 'org.jenkins-ci.main:jenkins-test-harness-tools:2.2'

    // Plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:5.14'
    testPlugins 'org.jenkins-ci.plugins:credentials:2.1.10'
    testPlugins 'org.jenkins-ci.plugins:cvs:2.13'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.40.0'
    testPlugins 'org.jenkins-ci.plugins:token-macro:2.5'
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:2.7'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs
}

